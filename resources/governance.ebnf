<document> = { block }
<block> = resolution | <comment>
<resolution> = ( create | update | delete | convert ), <".">, { ws }
create = <"create">, ws, entity, ws, name, [ws, <"with">, ws, create-components]
update = <"update">, ws, entity, ws, name, ws, update-action-list
delete = <"delete">, ws, entity, ws, name
convert = <"convert">, ws, entity, ws, name, ws, <"into a">, ws, entity
comment = <"--">, text, cr, { cr }
<cr> = <"\n">
<text> = #'[a-zA-Z0-9 :-]*'
<update-action-list> = update-action, { ws <"and"> ws update-action }
<update-action> = add | remove | change-purpose | rename
add = <"add">, ws, component
remove = <"remove">, ws, component
change-purpose = <"change purpose to">, ws, name
rename = <"rename to">, ws, name
<ws> = <#'\s+'>
<entity> = anchor-circle | circle | role
anchor-circle = <"anchor circle">
circle = <"circle">
role = <"role">
<name> = <'"'>, text, <'"'>
<create-components> = component, { ws <"and"> ws component }
<component> = purpose | accountabilities | domains | cross-links
purpose = <"purpose">, ws, name
accountabilities = <"accountability">, ws, name | <"accountabilities">, ws,
name-list
domains = <"domain">, ws, name | <"domains">, ws, name-list
cross-links = <"cross-link">, ws, name | <"cross-links">, ws, name-list
<name-list> = name, { <",">, ws, name }